import groovy.json.JsonSlurper

buildscript {
    def versionFile = file('versions.json')
    def json = new JsonSlurper().parseText(versionFile.text)

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.6.1'
        classpath 'com.github.maiflai:gradle-scalatest:0.5', 'org.scoverage:gradle-scoverage:1.0-5-g9c68988'
    }

    ext {
        appName = 'gameover-fwk'
        appVersion = json.version

        scalaVersion = json.scalaVersion
        gdxVersion = json.libGDXVersion
        scalaLangVersion = scalaVersion.substring(0, scalaVersion.lastIndexOf('.'))
        scalaTestVersion = json.scalaTestVersion
        scalaMockVersion = json.scalaMockVersion
        scoverageVersion = json.scoverageVersion
    }
}

plugins {
    id "com.github.maiflai.scalatest" version "0.14"
    id 'net.researchgate.release' version '2.2.1'
}

apply plugin: 'scala'
apply plugin: "com.github.maiflai.scalatest"
apply plugin: 'scoverage'
apply plugin: 'maven-publish'

apply from: 'publish.gradle'

defaultTasks 'build'

group = 'io.github.gameover-fwk'
archivesBaseName = "gameover-fwk_$scalaLangVersion"
version = "$appVersion"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "org.scala-lang:scala-library:$scalaVersion"
    testCompile "org.scalamock:scalamock-scalatest-support_$scalaLangVersion:$scalaMockVersion"
    testCompile "org.scalatest:scalatest_$scalaLangVersion:$scalaTestVersion"
    testRuntime 'org.pegdown:pegdown:1.1.0'
    testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    testCompile 'junit:junit:4.12'
    testRuntime "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    scoverage "org.scoverage:scalac-scoverage-plugin_$scalaLangVersion:$scoverageVersion",
            "org.scoverage:scalac-scoverage-runtime_$scalaLangVersion:$scoverageVersion"
}

compileScala {
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
    // activate incremental compilation
    scalaCompileOptions.additionalParameters = ["-language:postfixOps", "-feature"]//, "-Ylog-classpath"]
}

test {
    maxParallelForks = 1
}

task getVersion << {
  println version
}

jar.archiveName = "gameover-fwk_$scalaLangVersion"

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "gameover-fwk_$scalaLangVersion"

            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}
