buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.1"
    }
}

plugins {
    id "com.github.maiflai.scalatest" version "0.9"
    id 'net.researchgate.release' version '2.2.1'
}

apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: "com.github.maiflai.scalatest"
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'


group = 'io.github.gameover-fwk'
archivesBaseName = "gameover-fwk"
version = '0.2.1'

ext {
    gdxVersion = '1.5.4'
    scalaVersion = '2.11.5'
    scalaLangVersion = scalaVersion.substring(0, scalaVersion.lastIndexOf('.'))
    scalaTestVersion = '2.2.4'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "org.scala-lang:scala-library:$scalaVersion"
    testCompile "org.scalatest:scalatest_$scalaLangVersion:$scalaTestVersion"
    testRuntime 'org.pegdown:pegdown:1.1.0'
}

compileScala {
    scalaCompileOptions.useCompileDaemon = true

    // optionally specify host and port of the daemon:
    scalaCompileOptions.additionalParameters = ["-language:postfixOps", "-language:implicitConversions", "-feature"]//, "-Ylog-classpath"]
    scalaCompileOptions.daemonServer = "localhost:4243"
    scalaCompileOptions.useAnt = false
}

test {
    maxParallelForks = 1
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'gameover-fwk'

            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

configurations {
  deployerJars
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'gameover-fwk'
                packaging 'jar'
                artifactId 'gameover-fwk'
                description 'A framework to help developing games and UI with LibGDX and scala'
                url 'http://gameover-fwk.github.io/gameover-fwk/'

                scm {
                    connection 'scm:git:https://github.com/gameover-fwk/gameover-fwk/'
                    developerConnection 'scm:git:https://github.com/gameover-fwk/gameover-fwk/'
                    url 'https://github.com/gameover-fwk/gameover-fwk/'
                }

                licenses {
                    license {
                        name 'MIT license'
                        url 'https://en.wikipedia.org/wiki/MIT_License'
                    }
                }

                developers {
                    developer {
                        id 'pixelduck'
                        name 'Olivier Martin'
                        email 'pixelduck.game@gmail.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "io.github.gameover-fwk"
}

idea {
  module {
      name = "gameover-fwk"
      downloadJavadoc = false
      downloadSources = true
  }
}
